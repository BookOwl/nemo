use ast::{Expr, Op, Prototype, Definition};

grammar;

// Top level definitions

Prototype: Prototype = {
    <Name> "(" <Names> ")" => Prototype::new(<>),
};

pub Definition: Definition = {
    <Prototype> "=>" <Expr> => Definition::new(<>)
};

pub Program: Vec<Definition> = {
    Definition*,
};


// Expressions
pub Expr: Box<Expr> = {
    <Name> ":=" <Expr> => Box::new(Expr::Assignment(<>)),
    "push" <Expr> => Box::new(Expr::Push(<>)),
    "return" <Expr> => Box::new(Expr::Return(<>)),
    Closure,
};

Closure: Box<Expr> = {
    Lambda,
    Control,
};

Control: Box<Expr> = {
    If,
    While,
    BoolExpr,
};

BoolExpr: Box<Expr> = {
    BoolExpr BoolOp CompExpr => Box::new(Expr::Binary(<>)),
    CompExpr,
};

CompExpr: Box<Expr> = {
    CompExpr CompOp AddExpr => Box::new(Expr::Binary(<>)),
    AddExpr,
};

AddExpr: Box<Expr> = {
    AddExpr ExprOp Factor => Box::new(Expr::Binary(<>)),
    Factor,
};

Factor: Box<Expr> = {
    Factor FactorOp Pipe => Box::new(Expr::Binary(<>)),
    Pipe,
};

Pipe: Box<Expr> = {
    Pipe PipeOp Term => Box::new(Expr::Binary(<>)),
    Term,
};

Term: Box<Expr> = {
    Num => Box::new(Expr::Number(<>)),
    Str => Box::new(Expr::Str(<>)),
    Bool => Box::new(Expr::Bool(<>)),
    Name => Box::new(Expr::Name(<>)),
    "-" <Num> => Box::new(Expr::Neg(Box::new(Expr::Number(<>)))),
    "FinishedPipe" => Box::new(Expr::FinishedPipe),
    Call,
    Slice,
    "pull" => Box::new(Expr::Pull),
    "(" <Expr> ")",
    "{" <ExprStatements> "}" => Box::new(Expr::Block(<>))
};

Num: f64 = <s:r"[0-9]+(\.[0-9]+)?"> => s.parse::<f64>().unwrap();
Name: String = <s:r"[a-zA-Z_][a-zA-Z0-9_]*"> => String::from(s);
Str: String = <s:r"'[^']*'"> => s[1..(s.len()-1)].to_string();
Bool: bool = {
    "true" => true,
    "false" => false,
};

Call: Box<Expr> = {
    <Term> "(" <Exprs> ")" => Box::new(Expr::Call(<>)),
};

Lambda: Box<Expr> = {
    <n:Name> "->" <e:Expr> => Box::new(Expr::Lambda(vec![String::from(n)], e)),
    "|" <Names> "|" "->" <Expr> => Box::new(Expr::Lambda(<>)),
};

Slice: Box<Expr> = {
    <Term> "[" <Expr> "]" => Box::new(Expr::Index(<>)),
};
// Control flow expressions.

If: Box<Expr> = {
    "if" <Expr> "then" <Expr> "else" <Expr> => Box::new(Expr::If(<>))
};

While: Box<Expr> = {
    "while" <Expr> "do" <Expr> => Box::new(Expr::While(<>))
};


// Helpers

ExprOp: Op = {
    "+" => Op::Plus,
    "-" => Op::Minus,
};

FactorOp: Op = {
    "*" => Op::Times,
    "/" => Op::Slash,
    "%" => Op::Percent,
};

PipeOp: Op = {
    "|" => Op::Pipe,
};

CompOp: Op = {
    ">"  => Op::Greater,
    "<"  => Op::Lesser,
    "="  => Op::Equals,
    "!=" => Op::NotEquals,
};

BoolOp: Op = {
    "and" => Op::And,
    "or"  => Op::Or,
};

Exprs = Comma<Expr>;
Names = Comma<Name>;
ExprStatements = SemiCol<Expr>;

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

SemiCol<T>: Vec<T> = {
    <v:(<T> ";")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};
